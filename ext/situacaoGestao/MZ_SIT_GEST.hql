set hive.execution.engine=mr;
set hive.exec.parallel=true;
set hive.exec.reducers.max=90;

DROP TABLE IF EXISTS CD_IFRS9.MZ_SIT_GEST;
CREATE EXTERNAL TABLE CD_IFRS9.MZ_SIT_GEST(
PENUMPER STRING
,COD_ENTIDAD STRING
,COD_CENTRO STRING
,COD_PRODUCTO STRING
,COD_SUBPRODU STRING
,NUM_CUENTA STRING
,NUM_SECUENCIA_CTO STRING
,PUBLIC STRING
,DIGIT_01 STRING
,DIGIT_02 STRING
,DIGIT_03 STRING
,DIGIT_04 STRING
,DIGIT_05 STRING
,DT_REFE STRING
)
STORED AS TEXTFILE
LOCATION '/sistemas/ifr/ods/staging/MZ_SIT_GEST/MZ_SIT_GEST';

DROP TABLE IF EXISTS CD_IFRS9.MZ_PUBLIC_SIT_GEST_TEMP;
CREATE EXTERNAL TABLE CD_IFRS9.MZ_PUBLIC_SIT_GEST_TEMP(
PENUMPER STRING
,COD_ENTIDAD STRING
,COD_CENTRO STRING
,COD_PRODUCTO STRING
,COD_SUBPRODU STRING
,NUM_CUENTA STRING
,NUM_SECUENCIA_CTO STRING
,SUB_SEG STRING
,COD_PROD STRING
,DIAS_ATRS INT
,LIMIT BIGINT
,SIT_OPR STRING
,DT_REFE STRING
)STORED AS TEXTFILE
LOCATION '/sistemas/ifr/ods/staging/MZ_SIT_GEST/MZ_PUBLIC_SIT_GEST_TEMP';


INSERT INTO TABLE CD_IFRS9.MZ_PUBLIC_SIT_GEST_TEMP
SELECT CTNR.PENUMPER,
       MZ02.COD_ENTIDAD
       ,MZ02.COD_CENTRO
       ,MZ02.COD_PRODUCTO
       ,MZ02.COD_SUBPRODU
       ,MZ02.NUM_CUENTA
       ,MZ02.NUM_SECUENCIA_CTO
       ,CTNR.PESUBSEG SUB_SEG                                                          -- SUB_SEGMENTO PESSOA
       ,MZ02.COD_PRODUCTO COD_PROD                                                     -- CÃ“DIGO PRODUTO PESSOA
       ,MZ02.QT_ARS_PRC_MZ13 DIAS_ATRS                                                 -- DIAS EM ATRASO
       ,((MZ02.VL_ORI_OPR_MZ13 - MZ02.SLD_DVR_OPR_MZ13) + MZ02.SLD_DVR_OPR_MZ13) LIMIT --LIMITES "VL-ORI-OPR-MZ13 - SLD-DVR-OPR-MZ13" + SALDO DEVEDOR"SLD-DVR-OPR-MZ13"
       ,MZ02.SIT_OPR_CDT_MZ13 SIT_OPR
       ,MZ02.DT_REFE
FROM
     (
      SELECT COD_ENTIDAD
             ,COD_CENTRO
             ,COD_PRODUCTO
             ,COD_SUBPRODU
             ,NUM_CUENTA
             ,NUM_SECUENCIA_CTO
             ,CAST(QT_ARS_PRC_MZ13 AS INT) QT_ARS_PRC_MZ13
             ,CAST(VL_ORI_OPR_MZ13 AS BIGINT) VL_ORI_OPR_MZ13
             ,CAST(SLD_DVR_OPR_MZ13 AS BIGINT) SLD_DVR_OPR_MZ13
             ,CAST(SIT_OPR_CDT_MZ13 AS INT) SIT_OPR_CDT_MZ13
             ,DT_REFE
      FROM
           CD_IFRS9.INS_MZ0_0002_M
      WHERE
           DT_REFE = '20170531'
     ) MZ02
INNER JOIN
     (
      SELECT PENUMPER
             ,PESUBSEG
             ,DT_REFE
      FROM
           CD_IFRS9.PER_PE0_0001_M
      WHERE
           DT_REFE = '20170531'
     ) CTNR ON
     (
       CTNR.DT_REFE = MZ02.DT_REFE
     )
WHERE
     MZ02.DT_REFE = '20170531'
;



DROP TABLE IF EXISTS CD_IFRS9.MZ_PUBLIC_SIT_GEST_AUX;
CREATE EXTERNAL TABLE CD_IFRS9.MZ_PUBLIC_SIT_GEST_AUX(
PENUMPER STRING
,COD_ENTIDAD STRING
,COD_CENTRO STRING
,COD_PRODUCTO STRING
,COD_SUBPRODU STRING
,NUM_CUENTA STRING
,NUM_SECUENCIA_CTO STRING
,IND_ACRD INT
,SIST STRING
,ATRS_ACRD INT
,SIT_OPR INT
,CAT_RISC STRING
,CLS_RISC STRING
,DT_REFE STRING
)STORED AS TEXTFILE
LOCATION '/sistemas/ifr/ods/staging/MZ_SIT_GEST/MZ_PUBLIC_SIT_GEST_AUX';

INSERT INTO TABLE CD_IFRS9.MZ_PUBLIC_SIT_GEST_AUX
SELECT MZ_TEMP.PENUMPER
       ,MZ_TEMP.COD_ENTIDAD
       ,MZ_TEMP.COD_CENTRO
       ,MZ_TEMP.COD_PRODUCTO
       ,MZ_TEMP.COD_SUBPRODU
       ,MZ_TEMP.NUM_CUENTA
       ,MZ_TEMP.NUM_SECUENCIA_CTO
       ,MZ01.MZ100_IN_ACD_ATIVO IND_ACRD
       ,IF(LY.FEC_ALTA_ACUERDO IS NOT NULL,'LY',NULL) SIST
       ,(CAST(MZ_TEMP.DT_REFE AS INT) - LY.FEC_ALTA_ACUERDO) ATRS_ACRD
       ,MZ_TEMP.SIT_OPR
       ,MZ01.MZ100_CAT_RISC_FIN_AT CAT_RISC
       ,MZ01.MZ100_CLA_RISC_FIN_AT CLS_RISC
       ,MZ_TEMP.DT_REFE
FROM
     (
      SELECT PENUMPER
             ,COD_ENTIDAD
             ,COD_CENTRO
             ,COD_PRODUCTO
             ,COD_SUBPRODU
             ,NUM_CUENTA
             ,NUM_SECUENCIA_CTO
             ,SIT_OPR
             ,DT_REFE
      FROM
           CD_IFRS9.MZ_PUBLIC_SIT_GEST_TEMP
      WHERE
           DT_REFE = '20170531'
     ) MZ_TEMP
LEFT OUTER JOIN
     (
      SELECT COD_ENTIDAD
             ,COD_CENTRO
             ,COD_PRODUCTO
             ,COD_SUBPRODU
             ,NUM_CUENTA
             ,NUM_SECUENCIA_CTO
             ,DT_REFE
             ,CAST(MZ100_IN_ACD_ATIVO AS INT) MZ100_IN_ACD_ATIVO
             ,MZ100_CAT_RISC_FIN_AT
             ,MZ100_CLA_RISC_FIN_AT
      FROM
           CD_IFRS9.INS_MZ0_0001_M
      WHERE
           DT_REFE = '20170531'
     ) MZ01 ON
     (
          MZ_TEMP.COD_ENTIDAD       = MZ01.COD_ENTIDAD
      AND MZ_TEMP.COD_CENTRO        = MZ01.COD_CENTRO
      AND MZ_TEMP.COD_PRODUCTO      = MZ01.COD_PRODUCTO
      AND MZ_TEMP.COD_SUBPRODU      = MZ01.COD_SUBPRODU
      AND MZ_TEMP.NUM_CUENTA        = MZ01.NUM_CUENTA
      AND MZ_TEMP.NUM_SECUENCIA_CTO = MZ01.NUM_SECUENCIA_CTO
      AND MZ_TEMP.DT_REFE           = MZ01.DT_REFE
     )
LEFT OUTER JOIN
     (
      SELECT FEC_ALTA_ACUERDO
             ,COD_ENTIDAD
             ,COD_CENTRO
             ,COD_PRODUCTO
             ,COD_SUBPRODU
             ,NUM_CUENTA
             ,NUM_SECUENCIA_CTO
             ,DT_REFE
      FROM
           CD_IFRS9.EMP_LY0_0001_M
      WHERE
           DT_REFE = '20170531'
     ) LY ON
     (
          MZ_TEMP.COD_ENTIDAD       = LY.COD_ENTIDAD
      AND MZ_TEMP.COD_CENTRO        = LY.COD_CENTRO
      AND MZ_TEMP.COD_PRODUCTO      = LY.COD_PRODUCTO
      AND MZ_TEMP.COD_SUBPRODU      = LY.COD_SUBPRODU
      AND MZ_TEMP.NUM_CUENTA        = LY.NUM_CUENTA
      AND MZ_TEMP.NUM_SECUENCIA_CTO = LY.NUM_SECUENCIA_CTO
      AND MZ_TEMP.DT_REFE           = LY.DT_REFE
     )
WHERE
     MZ_TEMP.DT_REFE = '20170531'
;