--DIGITOS
set hive.execution.engine=mr;
set hive.exec.parallel=true;
set hive.exec.reducers.max=90;

CREATE TEMPORARY EXTERNAL TABLE CD_IFRS9.MZ_PUBLIC_UNIF(
PENUMPER STRING
,COD_ENTIDAD STRING
,COD_CENTRO STRING
,COD_PRODUCTO STRING
,COD_SUBPRODU STRING
,NUM_CUENTA STRING
,NUM_SECUENCIA_CTO STRING
,PUBLIC STRING
,DIGIT_01 STRING
,COD_PROD STRING
,DIAS_ATRS INT
,DNP_SUB STRING
,DT_REFE STRING
)
;




INSERT INTO TABLE CD_IFRS9.MZ_PUBLIC_UNIF
SELECT DISTINCT MZ_01.PENUMPER
,MZ_01.COD_ENTIDAD
,MZ_01.COD_CENTRO
,MZ_01.COD_PRODUCTO
,MZ_01.COD_SUBPRODU
,MZ_01.NUM_CUENTA
,MZ_01.NUM_SECUENCIA_CTO
,MZ_01.PUBLIC
,MZ_01.DIGIT_01
,MZ_01.COD_PROD
,MZ_01.DIAS_ATRS
,MZ_01.DNP_SUB
,MZ_01.DT_REFE
FROM
     CD_IFRS9.MZ_PUBLIC_1_1 MZ_01
WHERE
     MZ_01.DT_REFE = '20170531'
;

INSERT INTO TABLE CD_IFRS9.MZ_PUBLIC_UNIF
SELECT DISTINCT MZ_02.PENUMPER
,MZ_02.COD_ENTIDAD
,MZ_02.COD_CENTRO
,MZ_02.COD_PRODUCTO
,MZ_02.COD_SUBPRODU
,MZ_02.NUM_CUENTA
,MZ_02.NUM_SECUENCIA_CTO
,MZ_02.PUBLIC
,MZ_02.DIGIT_01
,MZ_02.COD_PROD
,MZ_02.DIAS_ATRS
,MZ_02.DNP_SUB
,MZ_02.DT_REFE
FROM
     CD_IFRS9.MZ_PUBLIC_2_1 MZ_02
WHERE
     MZ_02.DT_REFE = '20170531'
;



--UNIFICAÇÃO DOS PUBLICOS PARA A SITUAÇÃO DE GESTÃO
INSERT INTO CD_IFRS9.MZ_SIT_GEST
SELECT DISTINCT MZ_PUB_UNI.PENUMPER
,MZ_TEMP.COD_ENTIDAD
,MZ_TEMP.COD_CENTRO
,MZ_TEMP.COD_PRODUCTO
,MZ_TEMP.COD_SUBPRODU
,MZ_TEMP.NUM_CUENTA
,MZ_TEMP.NUM_SECUENCIA_CTO
,MZ_PUB_UNI.PUBLIC
,MZ_PUB_UNI.DIGIT_01 --MARCAÇÃO DE SITUAÇÃO DE DEFAULT
,CASE WHEN MZ_AUX.SIT_OPR = 4 THEN'3'           --PREJUÍZO
      WHEN MZ_PUB_UNI.DNP_SUB = 'DNP' THEN '2'      --DUDOSO
      WHEN MZ_AUX.CAT_RISC = 'MOR'
           AND MZ_AUX.CLS_RISC = 'ACD' THEN '1' -- MOROSO
      WHEN MZ_AUX.CAT_RISC = 'MOR'
           AND MZ_AUX.CLS_RISC = 'ATR' THEN '1' -- MOROSO
      WHEN MZ_AUX.CAT_RISC = 'MOR'
           AND MZ_AUX.CLS_RISC = 'ARR' THEN '4' --MOROSO POR ARRASTO
      WHEN MZ_AUX.CAT_RISC = 'SUB'
           AND MZ_AUX.CLS_RISC = 'ACD' THEN '5' --SUBSTANDARD ACORDO
      WHEN MZ_AUX.CAT_RISC = 'SUB'
           AND MZ_AUX.CLS_RISC = 'RNG' THEN '6' --SUBSTANDARD RENEGOCIAÇÃO
      WHEN MZ_AUX.CAT_RISC = 'SEG'
           AND MZ_AUX.CLS_RISC = 'ACD' THEN '7' --SEGMENTO ESPECIAL DEFINIÇÃO
      WHEN MZ_AUX.CAT_RISC = 'NOR'
           AND MZ_AUX.CLS_RISC = 'ACD' THEN '8' --NORMAL POR ACORDO DEFINIÇÃO
      WHEN MZ_AUX.CAT_RISC = 'NOR' THEN '9'     --NORMAL
      ELSE '0' --OUTROS
 END DIGIT_02 --MARCAÇÃO DE MOROSIDADE
,CASE WHEN MZ_PUB_UNI.DIAS_ATRS = 0 THEN '0'                 --EM DIA
      WHEN MZ_PUB_UNI.DIAS_ATRS BETWEEN 1 AND 14    THEN '1' --ATRASO DE 01 A 14 DIAS
      WHEN MZ_PUB_UNI.DIAS_ATRS BETWEEN 15 AND 30   THEN '2' --ATRASO DE 15 A 30 DIAS
      WHEN MZ_PUB_UNI.DIAS_ATRS BETWEEN 31 AND 60   THEN '3' --ATRASO DE 31 A 60 DIAS
      WHEN MZ_PUB_UNI.DIAS_ATRS BETWEEN 61 AND 90   THEN '4' --ATRASO DE 61 A 90 DIAS
      WHEN MZ_PUB_UNI.DIAS_ATRS BETWEEN 91 AND 120  THEN '5' --ATRASO DE 91 A 120 DIAS
      WHEN MZ_PUB_UNI.DIAS_ATRS BETWEEN 121 AND 150 THEN '6' --ATRASO DE 121 A 150 DIAS
      WHEN MZ_PUB_UNI.DIAS_ATRS BETWEEN 151 AND 180 THEN '7' --ATRASO DE 151 A 180 DIAS
      WHEN MZ_PUB_UNI.DIAS_ATRS BETWEEN 181 AND 360 THEN '8' --ATRASO DE 181 A 360 DIAS
      WHEN MZ_PUB_UNI.DIAS_ATRS > 360 THEN '9'               --ATRASO SUPERIOR A 360 DIAS
      ELSE '3'
 END DIGIT_03 --MARCAÇÃO DE FAIXA DE ATRASO 
,CASE WHEN MZ_AUX.IND_ACRD <> 0
           AND MZ_PUB_UNI.DIAS_ATRS > IF(MZ_AUX.SIST = 'LY',42,42) THEN '3' --ACORDO QUEBRADO
      WHEN MZ_AUX.IND_ACRD = 1 THEN '2'                                     --PRIMEIRO ACORDO EM CURSO
      WHEN MZ_AUX.IND_ACRD IN (4,5) THEN '6'                                --ACORDO EM CURSO DE CONTRATO EM PREJUIZO
      WHEN MZ_AUX.IND_ACRD = 3 THEN '5'                                     --SEGUNDO OU N-ESIMO ACORDO EM CURSO
      WHEN MZ_TEMP.COD_PROD IN ('1679','1708','1709','1711')
           AND MZ_AUX.IND_ACRD IS NULL THEN '1'                             --REFINANCIAMENTO
      WHEN MZ_AUX.IND_ACRD IN (0,2) THEN '4'                                --SEM ACORDO
      ELSE '4'
 END DIGIT_04 --MARCAÇÃO DE SITUAÇÃO DO ACORD
,CASE WHEN MZ_AUX.ATRS_ACRD = 0 THEN '0'                  --EM DIA
      WHEN MZ_AUX.ATRS_ACRD BETWEEN 1 AND 14    THEN '1'  --ATRASO DE 01 A 14 DIAS
      WHEN MZ_AUX.ATRS_ACRD BETWEEN 15 AND 30   THEN '2'  --ATRASO DE 15 A 30 DIAS
      WHEN MZ_AUX.ATRS_ACRD BETWEEN 31 AND 60   THEN '3'  --ATRASO DE 31 A 60 DIAS
      WHEN MZ_AUX.ATRS_ACRD BETWEEN 61 AND 90   THEN '4'  --ATRASO DE 61 A 90 DIAS
      WHEN MZ_AUX.ATRS_ACRD BETWEEN 91 AND 120  THEN '5'  --ATRASO DE 91 A 120 DIAS
      WHEN MZ_AUX.ATRS_ACRD BETWEEN 121 AND 150 THEN '6'  --ATRASO DE 121 A 150 DIAS
      WHEN MZ_AUX.ATRS_ACRD BETWEEN 151 AND 180 THEN '7'  --ATRASO DE 151 A 180 DIAS
      WHEN MZ_AUX.ATRS_ACRD BETWEEN 181 AND 360 THEN '8'  --ATRASO DE 181 A 360 DIAS
      WHEN MZ_AUX.ATRS_ACRD > 360 THEN '9'                --ATRASO SUPERIOR A 360 DIAS
      ELSE '0'
 END DIGIT_05 --MARCAÇÃO DE SITUAÇÃO DAS AÇÕES JUDICIAIS 
,MZ_PUB_UNI.DT_REFE
FROM
     CD_IFRS9.MZ_PUBLIC_SIT_GEST_TEMP MZ_TEMP
INNER JOIN
     CD_IFRS9.MZ_PUBLIC_UNIF MZ_PUB_UNI ON
     (
          MZ_TEMP.PENUMPER          = MZ_PUB_UNI.PENUMPER
      AND MZ_TEMP.DT_REFE           = MZ_PUB_UNI.DT_REFE
      AND MZ_TEMP.COD_ENTIDAD       = MZ_PUB_UNI.COD_ENTIDAD
      AND MZ_TEMP.COD_CENTRO        = MZ_PUB_UNI.COD_CENTRO
      AND MZ_TEMP.COD_PRODUCTO      = MZ_PUB_UNI.COD_PRODUCTO
      AND MZ_TEMP.COD_SUBPRODU      = MZ_PUB_UNI.COD_SUBPRODU
      AND MZ_TEMP.NUM_CUENTA        = MZ_PUB_UNI.NUM_CUENTA
      AND MZ_TEMP.NUM_SECUENCIA_CTO = MZ_PUB_UNI.NUM_SECUENCIA_CTO
     )
INNER JOIN
     CD_IFRS9.MZ_PUBLIC_SIT_GEST_AUX MZ_AUX ON
     (
          MZ_TEMP.PENUMPER          = MZ_AUX.PENUMPER
      AND MZ_TEMP.DT_REFE           = MZ_AUX.DT_REFE
      AND MZ_TEMP.COD_ENTIDAD       = MZ_AUX.COD_ENTIDAD
      AND MZ_TEMP.COD_CENTRO        = MZ_AUX.COD_CENTRO
      AND MZ_TEMP.COD_PRODUCTO      = MZ_AUX.COD_PRODUCTO
      AND MZ_TEMP.COD_SUBPRODU      = MZ_AUX.COD_SUBPRODU
      AND MZ_TEMP.NUM_CUENTA        = MZ_AUX.NUM_CUENTA
      AND MZ_TEMP.NUM_SECUENCIA_CTO = MZ_AUX.NUM_SECUENCIA_CTO
     )
WHERE
     MZ_TEMP.DT_REFE = '20170531'
;